<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Product.Categorical.Right</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Right-biased universe-sensitive functor and monad instances for the</a>
<a id="177" class="Comment">-- Product type.</a>
<a id="194" class="Comment">--</a>
<a id="197" class="Comment">-- To minimize the universe level of the RawFunctor, we require that</a>
<a id="266" class="Comment">-- elements of B are &quot;lifted&quot; to a copy of B at a higher universe level</a>
<a id="338" class="Comment">-- (a ⊔ b). See the Data.Product.Categorical.Examples for how this is</a>
<a id="408" class="Comment">-- done.</a>
<a id="417" class="Comment">------------------------------------------------------------------------</a>

<a id="491" class="Symbol">{-#</a> <a id="495" class="Keyword">OPTIONS</a> <a id="503" class="Pragma">--without-K</a> <a id="515" class="Pragma">--safe</a> <a id="522" class="Symbol">#-}</a>

<a id="527" class="Keyword">open</a> <a id="532" class="Keyword">import</a> <a id="539" href="Algebra.html" class="Module">Algebra</a>
<a id="547" class="Keyword">open</a> <a id="552" class="Keyword">import</a> <a id="559" href="Level.html" class="Module">Level</a>

<a id="566" class="Keyword">module</a> <a id="573" href="Data.Product.Categorical.Right.html" class="Module">Data.Product.Categorical.Right</a>
  <a id="606" class="Symbol">(</a><a id="607" href="Data.Product.Categorical.Right.html#607" class="Bound">a</a> <a id="609" class="Symbol">:</a> <a id="611" href="Agda.Primitive.html#423" class="Postulate">Level</a><a id="616" class="Symbol">)</a> <a id="618" class="Symbol">{</a><a id="619" href="Data.Product.Categorical.Right.html#619" class="Bound">b</a> <a id="621" href="Data.Product.Categorical.Right.html#621" class="Bound">e</a><a id="622" class="Symbol">}</a> <a id="624" class="Symbol">(</a><a id="625" href="Data.Product.Categorical.Right.html#625" class="Bound">B</a> <a id="627" class="Symbol">:</a> <a id="629" href="Algebra.Bundles.html#4104" class="Record">RawMonoid</a> <a id="639" href="Data.Product.Categorical.Right.html#619" class="Bound">b</a> <a id="641" href="Data.Product.Categorical.Right.html#621" class="Bound">e</a><a id="642" class="Symbol">)</a> <a id="644" class="Keyword">where</a>

<a id="651" class="Keyword">open</a> <a id="656" class="Keyword">import</a> <a id="663" href="Data.Product.html" class="Module">Data.Product</a>
<a id="676" class="Keyword">import</a> <a id="683" href="Data.Product.Categorical.Right.Base.html" class="Module">Data.Product.Categorical.Right.Base</a> <a id="719" class="Symbol">as</a> <a id="722" class="Module">Base</a>
<a id="727" class="Keyword">open</a> <a id="732" class="Keyword">import</a> <a id="739" href="Category.Applicative.html" class="Module">Category.Applicative</a> <a id="760" class="Keyword">using</a> <a id="766" class="Symbol">(</a><a id="767" href="Category.Applicative.html#494" class="Function">RawApplicative</a><a id="781" class="Symbol">)</a>
<a id="783" class="Keyword">open</a> <a id="788" class="Keyword">import</a> <a id="795" href="Category.Monad.html" class="Module">Category.Monad</a> <a id="810" class="Keyword">using</a> <a id="816" class="Symbol">(</a><a id="817" href="Category.Monad.html#448" class="Function">RawMonad</a><a id="825" class="Symbol">;</a> <a id="827" href="Category.Monad.html#527" class="Function">RawMonadT</a><a id="836" class="Symbol">)</a>
<a id="838" class="Keyword">open</a> <a id="843" class="Keyword">import</a> <a id="850" href="Function.Base.html" class="Module">Function.Base</a> <a id="864" class="Keyword">using</a> <a id="870" class="Symbol">(</a><a id="871" href="Function.Base.html#615" class="Function">id</a><a id="873" class="Symbol">;</a> <a id="875" href="Function.Base.html#1554" class="Function">flip</a><a id="879" class="Symbol">;</a> <a id="881" href="Function.Base.html#1031" class="Function Operator">_∘_</a><a id="884" class="Symbol">;</a> <a id="886" href="Function.Base.html#3706" class="Function Operator">_∘′_</a><a id="890" class="Symbol">)</a>
<a id="892" class="Keyword">import</a> <a id="899" href="Function.Identity.Categorical.html" class="Module">Function.Identity.Categorical</a> <a id="929" class="Symbol">as</a> <a id="932" class="Module">Id</a>

<a id="936" class="Keyword">open</a> <a id="941" href="Algebra.Bundles.html#4104" class="Module">RawMonoid</a> <a id="951" href="Data.Product.Categorical.Right.html#625" class="Bound">B</a>

<a id="954" class="Comment">------------------------------------------------------------------------</a>
<a id="1027" class="Comment">-- Re-export the base contents publically</a>

<a id="1070" class="Keyword">open</a> <a id="1075" href="Data.Product.Categorical.Right.Base.html" class="Module">Base</a> <a id="1080" href="Algebra.Bundles.html#4186" class="Field">Carrier</a> <a id="1088" href="Data.Product.Categorical.Right.html#607" class="Bound">a</a> <a id="1090" class="Keyword">public</a>

<a id="1098" class="Comment">------------------------------------------------------------------------</a>
<a id="1171" class="Comment">-- Basic records</a>

<a id="applicative"></a><a id="1189" href="Data.Product.Categorical.Right.html#1189" class="Function">applicative</a> <a id="1201" class="Symbol">:</a> <a id="1203" href="Category.Applicative.html#494" class="Function">RawApplicative</a> <a id="1218" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a>
<a id="1227" href="Data.Product.Categorical.Right.html#1189" class="Function">applicative</a> <a id="1239" class="Symbol">=</a> <a id="1241" class="Keyword">record</a>
  <a id="1250" class="Symbol">{</a> <a id="1252" href="Category.Applicative.Indexed.html#1003" class="Field">pure</a> <a id="1257" class="Symbol">=</a> <a id="1259" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,</a> <a id="1262" href="Algebra.Bundles.html#4260" class="Field">ε</a>
  <a id="1266" class="Symbol">;</a> <a id="1268" href="Category.Applicative.Indexed.html#1034" class="Field Operator">_⊛_</a>  <a id="1273" class="Symbol">=</a> <a id="1275" href="Data.Product.html#3060" class="Function">zip</a> <a id="1279" href="Function.Base.html#615" class="Function">id</a> <a id="1282" href="Algebra.Bundles.html#4234" class="Field Operator">_∙_</a>
  <a id="1288" class="Symbol">}</a>

<a id="monadT"></a><a id="1291" href="Data.Product.Categorical.Right.html#1291" class="Function">monadT</a> <a id="1298" class="Symbol">:</a> <a id="1300" href="Category.Monad.html#527" class="Function">RawMonadT</a> <a id="1310" class="Symbol">(</a><a id="1311" href="Function.Base.html#3706" class="Function Operator">_∘′</a> <a id="1315" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a><a id="1323" class="Symbol">)</a>
<a id="1325" href="Data.Product.Categorical.Right.html#1291" class="Function">monadT</a> <a id="1332" href="Data.Product.Categorical.Right.html#1332" class="Bound">M</a> <a id="1334" class="Symbol">=</a> <a id="1336" class="Keyword">record</a>
  <a id="1345" class="Symbol">{</a> <a id="1347" href="Category.Monad.Indexed.html#644" class="Field">return</a> <a id="1354" class="Symbol">=</a> <a id="1356" href="Category.Applicative.Indexed.html#1003" class="Function">pure</a> <a id="1361" href="Function.Base.html#3706" class="Function Operator">∘′</a> <a id="1364" class="Symbol">(</a><a id="1365" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,</a> <a id="1368" href="Algebra.Bundles.html#4260" class="Field">ε</a><a id="1369" class="Symbol">)</a>
  <a id="1373" class="Symbol">;</a> <a id="1375" href="Category.Monad.Indexed.html#677" class="Field Operator">_&gt;&gt;=_</a>  <a id="1382" class="Symbol">=</a> <a id="1384" class="Symbol">λ</a> <a id="1386" href="Data.Product.Categorical.Right.html#1386" class="Bound">ma</a> <a id="1389" href="Data.Product.Categorical.Right.html#1389" class="Bound">f</a> <a id="1391" class="Symbol">→</a> <a id="1393" href="Data.Product.Categorical.Right.html#1386" class="Bound">ma</a> <a id="1396" href="Category.Monad.Indexed.html#677" class="Field Operator">&gt;&gt;=</a> <a id="1400" href="Data.Product.html#3422" class="Function">uncurry</a> <a id="1408" class="Symbol">λ</a> <a id="1410" href="Data.Product.Categorical.Right.html#1410" class="Bound">x</a> <a id="1412" href="Data.Product.Categorical.Right.html#1412" class="Bound">b</a> <a id="1414" class="Symbol">→</a> <a id="1416" href="Data.Product.html#2675" class="Function">map₂</a> <a id="1421" class="Symbol">(</a><a id="1422" href="Data.Product.Categorical.Right.html#1412" class="Bound">b</a> <a id="1424" href="Algebra.Bundles.html#4234" class="Field Operator">∙_</a><a id="1426" class="Symbol">)</a> <a id="1428" href="Category.Functor.html#608" class="Function Operator">&lt;$&gt;</a> <a id="1432" href="Data.Product.Categorical.Right.html#1389" class="Bound">f</a> <a id="1434" href="Data.Product.Categorical.Right.html#1410" class="Bound">x</a>
  <a id="1438" class="Symbol">}</a> <a id="1440" class="Keyword">where</a> <a id="1446" class="Keyword">open</a> <a id="1451" href="Category.Monad.html#832" class="Module">RawMonad</a> <a id="1460" href="Data.Product.Categorical.Right.html#1332" class="Bound">M</a>

<a id="monad"></a><a id="1463" href="Data.Product.Categorical.Right.html#1463" class="Function">monad</a> <a id="1469" class="Symbol">:</a> <a id="1471" href="Category.Monad.html#448" class="Function">RawMonad</a> <a id="1480" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a>
<a id="1489" href="Data.Product.Categorical.Right.html#1463" class="Function">monad</a> <a id="1495" class="Symbol">=</a> <a id="1497" href="Data.Product.Categorical.Right.html#1291" class="Function">monadT</a> <a id="1504" href="Function.Identity.Categorical.html#651" class="Function">Id.monad</a>

<a id="1514" class="Comment">------------------------------------------------------------------------</a>
<a id="1587" class="Comment">-- Get access to other monadic functions</a>

<a id="1629" class="Keyword">module</a> <a id="TraversableA"></a><a id="1636" href="Data.Product.Categorical.Right.html#1636" class="Module">TraversableA</a> <a id="1649" class="Symbol">{</a><a id="1650" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a><a id="1651" class="Symbol">}</a> <a id="1653" class="Symbol">(</a><a id="1654" href="Data.Product.Categorical.Right.html#1654" class="Bound">App</a> <a id="1658" class="Symbol">:</a> <a id="1660" href="Category.Applicative.html#494" class="Function">RawApplicative</a> <a id="1675" class="Symbol">{</a><a id="1676" href="Data.Product.Categorical.Right.html#607" class="Bound">a</a> <a id="1678" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="1680" href="Data.Product.Categorical.Right.html#619" class="Bound">b</a><a id="1681" class="Symbol">}</a> <a id="1683" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a><a id="1684" class="Symbol">)</a> <a id="1686" class="Keyword">where</a>

  <a id="1695" class="Keyword">open</a> <a id="1700" href="Category.Applicative.html#602" class="Module">RawApplicative</a> <a id="1715" href="Data.Product.Categorical.Right.html#1654" class="Bound">App</a>

  <a id="TraversableA.sequenceA"></a><a id="1722" href="Data.Product.Categorical.Right.html#1722" class="Function">sequenceA</a> <a id="1732" class="Symbol">:</a> <a id="1734" class="Symbol">∀</a> <a id="1736" class="Symbol">{</a><a id="1737" href="Data.Product.Categorical.Right.html#1737" class="Bound">A</a><a id="1738" class="Symbol">}</a> <a id="1740" class="Symbol">→</a> <a id="1742" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1751" class="Symbol">(</a><a id="1752" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1754" href="Data.Product.Categorical.Right.html#1737" class="Bound">A</a><a id="1755" class="Symbol">)</a> <a id="1757" class="Symbol">→</a> <a id="1759" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1761" class="Symbol">(</a><a id="1762" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1771" href="Data.Product.Categorical.Right.html#1737" class="Bound">A</a><a id="1772" class="Symbol">)</a>
  <a id="1776" href="Data.Product.Categorical.Right.html#1722" class="Function">sequenceA</a> <a id="1786" class="Symbol">(</a><a id="1787" href="Data.Product.Categorical.Right.html#1787" class="Bound">fa</a> <a id="1790" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1792" href="Data.Product.Categorical.Right.html#1792" class="Bound">y</a><a id="1793" class="Symbol">)</a> <a id="1795" class="Symbol">=</a> <a id="1797" class="Symbol">(</a><a id="1798" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,</a> <a id="1801" href="Data.Product.Categorical.Right.html#1792" class="Bound">y</a><a id="1802" class="Symbol">)</a> <a id="1804" href="Category.Functor.html#608" class="Function Operator">&lt;$&gt;</a> <a id="1808" href="Data.Product.Categorical.Right.html#1787" class="Bound">fa</a>

  <a id="TraversableA.mapA"></a><a id="1814" href="Data.Product.Categorical.Right.html#1814" class="Function">mapA</a> <a id="1819" class="Symbol">:</a> <a id="1821" class="Symbol">∀</a> <a id="1823" class="Symbol">{</a><a id="1824" href="Data.Product.Categorical.Right.html#1824" class="Bound">A</a> <a id="1826" href="Data.Product.Categorical.Right.html#1826" class="Bound">B</a><a id="1827" class="Symbol">}</a> <a id="1829" class="Symbol">→</a> <a id="1831" class="Symbol">(</a><a id="1832" href="Data.Product.Categorical.Right.html#1824" class="Bound">A</a> <a id="1834" class="Symbol">→</a> <a id="1836" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1838" href="Data.Product.Categorical.Right.html#1826" class="Bound">B</a><a id="1839" class="Symbol">)</a> <a id="1841" class="Symbol">→</a> <a id="1843" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1852" href="Data.Product.Categorical.Right.html#1824" class="Bound">A</a> <a id="1854" class="Symbol">→</a> <a id="1856" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1858" class="Symbol">(</a><a id="1859" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1868" href="Data.Product.Categorical.Right.html#1826" class="Bound">B</a><a id="1869" class="Symbol">)</a>
  <a id="1873" href="Data.Product.Categorical.Right.html#1814" class="Function">mapA</a> <a id="1878" href="Data.Product.Categorical.Right.html#1878" class="Bound">f</a> <a id="1880" class="Symbol">=</a> <a id="1882" href="Data.Product.Categorical.Right.html#1722" class="Function">sequenceA</a> <a id="1892" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="1894" href="Data.Product.html#2625" class="Function">map₁</a> <a id="1899" href="Data.Product.Categorical.Right.html#1878" class="Bound">f</a>

  <a id="TraversableA.forA"></a><a id="1904" href="Data.Product.Categorical.Right.html#1904" class="Function">forA</a> <a id="1909" class="Symbol">:</a> <a id="1911" class="Symbol">∀</a> <a id="1913" class="Symbol">{</a><a id="1914" href="Data.Product.Categorical.Right.html#1914" class="Bound">A</a> <a id="1916" href="Data.Product.Categorical.Right.html#1916" class="Bound">B</a><a id="1917" class="Symbol">}</a> <a id="1919" class="Symbol">→</a> <a id="1921" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1930" href="Data.Product.Categorical.Right.html#1914" class="Bound">A</a> <a id="1932" class="Symbol">→</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Data.Product.Categorical.Right.html#1914" class="Bound">A</a> <a id="1937" class="Symbol">→</a> <a id="1939" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1941" href="Data.Product.Categorical.Right.html#1916" class="Bound">B</a><a id="1942" class="Symbol">)</a> <a id="1944" class="Symbol">→</a> <a id="1946" href="Data.Product.Categorical.Right.html#1650" class="Bound">F</a> <a id="1948" class="Symbol">(</a><a id="1949" href="Data.Product.Categorical.Right.Base.html#901" class="Function">Productᵣ</a> <a id="1958" href="Data.Product.Categorical.Right.html#1916" class="Bound">B</a><a id="1959" class="Symbol">)</a>
  <a id="1963" href="Data.Product.Categorical.Right.html#1904" class="Function">forA</a> <a id="1968" class="Symbol">=</a> <a id="1970" href="Function.Base.html#1554" class="Function">flip</a> <a id="1975" href="Data.Product.Categorical.Right.html#1814" class="Function">mapA</a>

<a id="1981" class="Keyword">module</a> <a id="TraversableM"></a><a id="1988" href="Data.Product.Categorical.Right.html#1988" class="Module">TraversableM</a> <a id="2001" class="Symbol">{</a><a id="2002" href="Data.Product.Categorical.Right.html#2002" class="Bound">M</a><a id="2003" class="Symbol">}</a> <a id="2005" class="Symbol">(</a><a id="2006" href="Data.Product.Categorical.Right.html#2006" class="Bound">Mon</a> <a id="2010" class="Symbol">:</a> <a id="2012" href="Category.Monad.html#448" class="Function">RawMonad</a> <a id="2021" class="Symbol">{</a><a id="2022" href="Data.Product.Categorical.Right.html#607" class="Bound">a</a> <a id="2024" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="2026" href="Data.Product.Categorical.Right.html#619" class="Bound">b</a><a id="2027" class="Symbol">}</a> <a id="2029" href="Data.Product.Categorical.Right.html#2002" class="Bound">M</a><a id="2030" class="Symbol">)</a> <a id="2032" class="Keyword">where</a>

  <a id="2041" class="Keyword">open</a> <a id="2046" href="Category.Monad.html#832" class="Module">RawMonad</a> <a id="2055" href="Data.Product.Categorical.Right.html#2006" class="Bound">Mon</a>

  <a id="2062" class="Keyword">open</a> <a id="2067" href="Data.Product.Categorical.Right.html#1636" class="Module">TraversableA</a> <a id="2080" href="Category.Monad.Indexed.html#1141" class="Function">rawIApplicative</a> <a id="2096" class="Keyword">public</a>
    <a id="2107" class="Keyword">renaming</a>
    <a id="2120" class="Symbol">(</a> sequenceA <a id="2132" class="Symbol">to</a> sequenceM
    <a id="2149" class="Symbol">;</a> mapA      <a id="2161" class="Symbol">to</a> mapM
    <a id="2173" class="Symbol">;</a> forA      <a id="2185" class="Symbol">to</a> forM
    <a id="2197" class="Symbol">)</a>
</pre></body></html>